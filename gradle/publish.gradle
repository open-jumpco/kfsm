/*
    Copyright 2019-2021 Open JumpCO

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated
    documentation files (the "Software"), to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software,
    and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO
    THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
    CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
 */

apply plugin: 'signing'

def useTarget = project.ext.useTarget

task javadocJar(type: Jar) {
    archivesBaseName = 'kfsm'
    archiveClassifier = 'javadoc'
    from dokkaHtml.outputDirectory
}


gradle.afterProject {
    tasks.forEach { task ->
        if (task.name.contains('dokka')) {
            javadocJar.dependsOn(task)
        }
        if (task.name.contains('Publication')) {
            publish.dependsOn(task)
        }
    }
}


artifacts {
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name 'Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'corneil_jumpco'
            name 'Corneil @ JumpCO'
            organization 'Open JumpCO'
            organizationUrl 'https://open.jumpco.io'
        }
    }

    scm {
        url project.vcs
    }
}

def configureMavenCentralMetadata = { pom ->
    def root = asNode()
    root.appendNode('name', project.name)
    root.appendNode('description', project.description)
    root.appendNode('url', project.vcs)
    root.children().last() + pomConfig
}

publishing {
    repositories {
        maven {
            logger.lifecycle("maven:publish:version=$version")
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            url = project.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username rootProject.ext.find('ossJiraUser') ?: System.getenv('OSS_JIRA_USER')
                password rootProject.ext.find('ossJiraPwd') ?: System.getenv('OSS_JIRA_PWD')
            }
        }
    }
    def platforms = ['js', 'jvm', 'linux', 'mingw', 'macos', 'wasm']
    publications.all { publication ->
        pom.withXml(configureMavenCentralMetadata)
        logger.info "publication:$publication.name"
        if (platforms.contains(publication.name)) {
            publication.artifact javadocJar
        }
    }
}


signing {
    sign publishing.publications.kotlinMultiplatform
    // sign publishing.publications.metadata
    sign publishing.publications.documentation

    if (useTarget['jvm']) {
        sign publishing.publications.jvm
    }
    if (useTarget['js']) {
        sign publishing.publications.js
    }
    if (useTarget['macos']) {
        sign publishing.publications.macos
    }
    if (useTarget['linux']) {
        sign publishing.publications.linux
    }
    if (useTarget['mingw']) {
        sign publishing.publications.mingw
    }
    if (useTarget['wasm']) {
        sign publishing.publications.wasm
    }
}
